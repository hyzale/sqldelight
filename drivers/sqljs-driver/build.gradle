plugins {
  alias(libs.plugins.kotlin.multiplatform)
  alias(libs.plugins.publish)
  alias(libs.plugins.dokka)
}

kotlin {
  js {
    nodejs {
      testTask {
        useMocha {
          timeout = "5s"
        }
        filter.excludeTestsMatching("*JsWorker*")
      }
    }
    browser {
      testTask {
        useKarma {
          useChromeHeadless()
        }
      }
    }
  }

  sourceSets {
    jsMain {
      dependencies {
        api project(":runtime")
        implementation libs.kotlin.coroutines.core
        implementation libs.stately.concurrency
      }
    }

    jsTest {
      dependencies {
        implementation libs.kotlin.test.js
        implementation npm('sql.js', libs.versions.sqljs.get())
        implementation devNpm("copy-webpack-plugin", "9.1.0")
        implementation libs.kotlin.coroutines.test
        implementation project(':extensions:async-extensions')
      }
    }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
